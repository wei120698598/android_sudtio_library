apply plugin: 'com.android.library'


//依赖github和bintary库
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"



    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}



//生成jar
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs //通过from函数指定代码源，这里是默认代码源
    classifier 'sources'
}

//生成文档
task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs //source指定了代码源
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}



//这里dependsOn意为仅当generateJavadocs完成后才开始本task
task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}


//为了将你生成的源代码和Javadoc Jar文件添加到Maven中，你需要将task添加到一个archive中
artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

//license属性，马上就要说到如何选择一个license。license只是声明别人在使用你的代码时的权限，不需要申请，选择之后直接在项目中添加一个相应的LICENSE文件即可。除此之外，

//需要在bintray.properties文件中取值
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


group = 'com.wei.view' // 这里需要和真实包名对应，不能随便填写
version = '1.0.1' // 指定版本号

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'maven'
        name = 'com.wei.view'

        version {
            name = '1.0.1'
            desc = "decent banner"
            released = new Date()
            vcsTag = '1.0.1'
        }
    }

    configurations = ['archives']
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile project(':as_lib_CustomerUtils')
}
